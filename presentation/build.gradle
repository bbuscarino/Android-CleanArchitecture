apply plugin: 'com.android.application'

android {
  def globalConfiguration = rootProject.extensions.getByName("ext")

  compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
  buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

  defaultConfig {
    minSdkVersion globalConfiguration["androidMinSdkVersion"]
    targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

    applicationId globalConfiguration["androidApplicationId"]
    versionCode globalConfiguration["androidVersionCode"]
    versionName globalConfiguration["androidVersionName"]
    testInstrumentationRunner globalConfiguration["testInstrumentationRunner"]
    testApplicationId globalConfiguration["testApplicationId"]
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }

  lintOptions {
    quiet true
    abortOnError false
    ignoreWarnings true
    disable 'InvalidPackage'            //Some libraries have issues with this.
    disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be Android L Beta.
    disable 'IconDensities'             //For testing purpose. This is safe to remove.
    disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
  }
  testOptions {
    execution 'ANDROID_TEST_ORCHESTRATOR'
  }
  signingConfigs {
    debug {
      storeFile file('../buildsystem/debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }
  }
}

dependencies {
  def presentationDependencies = rootProject.ext.presentationDependencies
  def presentationTestDependencies = rootProject.ext.presentationTestDependencies
  def developmentDependencies = rootProject.ext.developmentDependencies

  compile project(':domain')
  compile project(':data')

  annotationProcessor presentationDependencies.daggerCompiler
  annotationProcessor presentationDependencies.butterKnife
  compile presentationDependencies.dagger
  compile presentationDependencies.butterKnife
  compile presentationDependencies.recyclerView
  compile presentationDependencies.rxJava
  compile presentationDependencies.rxAndroid
  compile presentationDependencies.picasso
  provided presentationDependencies.javaxAnnotation

  androidTestAnnotationProcessor presentationDependencies.butterKnife
  testAnnotationProcessor presentationDependencies.butterKnife
  androidTestCompile presentationTestDependencies.dexmaker
  androidTestCompile presentationTestDependencies.dexmakerMockito
  androidTestCompile presentationTestDependencies.espresso
  androidTestCompile presentationTestDependencies.testRules
  androidTestImplementation presentationTestDependencies.testRunner
  androidTestUtil presentationTestDependencies.testOrchestrator

  //Development
  compile developmentDependencies.leakCanary
}
